apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'com.github.xiaojinzi123'
archivesBaseName = "kcomponent"

android {

    namespace 'com.xiaojinzi.component.impl'

    compileSdkVersion versions.android_compile_sdk
    defaultConfig {
        minSdkVersion versions.android_min_sdk
        targetSdkVersion versions.android_target_sdk
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
        }
    }

}

dependencies {
    api libs.appcompat
    api libs.lifecycle_runtime_ktx
    api libs.kotlin_reflect
    api project(':ComponentApi')
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = group
                artifactId = archivesBaseName
                version = version
            }
        }
    }
}
